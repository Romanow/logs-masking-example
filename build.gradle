plugins {
    id "idea"
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "org.jetbrains.kotlin.plugin.spring" version "2.0.20"
    id "org.jetbrains.kotlin.kapt" version "1.9.22"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1"
    id "org.springframework.boot" version "3.3.3"
    id "io.spring.dependency-management" version "1.1.6"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
}

ext {
    springBootVersion = "3.3.3"
    logstashLayoutVersion = "1.0.5"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

repositories {
    mavenCentral()
}

ktlint {
    version.set("1.2.1")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
}

configurations {
    implementation {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

dependencies {
    implementation project("log-masking-lib")
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"

    implementation "org.apache.logging.log4j:log4j-layout-template-json"
    kapt "org.apache.logging.log4j:log4j-core"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport

processResources {
    filesMatching(["**/properties-from-gradle.properties"]) {
        expand(project.properties)
    }
}

gitProperties {
    dateFormat = "dd.MM.yyyy HH:mm:ss"
    dateFormatTimeZone = "Europe/Moscow"
    extProperty = "gitProps"
}

generateGitProperties.outputs.upToDateWhen { false }

bootJar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes(
            "Application-Name": "${project.name}",
            "Application-Version": "${project.version}",
            "Git-Branch": "${-> project.ext.gitProps["git.branch"]}",
            "Build-Revision": "${-> project.ext.gitProps["git.commit.id.abbrev"]}",
            "Commit-Time": "${-> project.ext.gitProps["git.commit.time"]}"
        )
    }
}

jar {
    enabled = false
}
